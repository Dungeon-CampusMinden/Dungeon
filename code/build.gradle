plugins {
    id "java"
    id "com.github.spotbugs" version "4.7.9" apply true
    id "eclipse"
    id "java-library"
    id "maven-publish"
    id "signing"
    id "io.codearte.nexus-staging" version "0.30.0"
}

group = "io.github.pm-dungeon"
version = "1.0"
ext {
    appName = "pmdungeon"
    gdxVersion = "1.10.0"
    roboVMVersion = "2.3.12"
    box2DLightsVersion = "1.5"
    ashleyVersion = "1.7.3"
    aiVersion = "1.8.2"
    gdxControllersVersion = "2.1.0"
}

sourceCompatibility = 17
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"
sourceSets.main.java.srcDirs = ["core/src/"]
sourceSets.test.java.srcDirs = ["core/test/"]
eclipse.project.name = appName + "-core"

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    google()
}

dependencies {
    // LibGDX
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

    // JUnit 4, Mockito and Powermock for testing:
    // https://mvnrepository.com/artifact/junit/junit
    testImplementation "junit:junit:4.13.2"

    // https://mvnrepository.com/artifact/org.powermock/powermock-module-junit4
    testImplementation "org.powermock:powermock-module-junit4:2.0.9"
    // https://mvnrepository.com/artifact/org.powermock/powermock-api-mockito2
    testImplementation "org.powermock:powermock-api-mockito2:2.0.9"
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

spotbugs {
    toolVersion = "4.2.2"
    ignoreFailures = true
    showStackTraces = false
}

spotbugsMain {
    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugsXml.xml")
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "io.github.pm-dungeon"
            artifactId = "core"
            version = "1.0.7"

            from components.java

            pom {
                name = "PM-Dungeon core"
                description = "The pmdungeon is a lightweight, libgdx based, Java framework for the development of a 2D rogue like."
                url = "https://github.com/PM-Dungeon/core"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/PM-Dungeon/core/blob/master/LICENSE.md"
                    }
                }
                developers {
                    developer {
                        id = "AMatutat"
                        name = "Andr√© Matutat"
                        email = ""
                    }
                    developer {
                        id = "cagix"
                        name = "Carsten Gips"
                        email = ""
                    }
                    developer {
                        id = "Tobias321"
                        name = "Tobias Grothe"
                        email = ""
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/PM-Dungeon/core.git"
                    developerConnection = ""
                    url = "https://github.com/PM-Dungeon/core"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    useInMemoryPgpKeys("$System.env.GPG_PRIVATE_KEY", "$System.env.PASSPHRASE")
    sign publishing.publications
}

tasks.withType(Javadoc) {
    // failOnError false
    options.addStringOption("Xdoclint:none", "-quiet")
    options.addStringOption("encoding", "UTF-8")
    options.addStringOption("charSet", "UTF-8")
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

nexusStaging {
    // required only for projects registered in Sonatype after 2021-02-24
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
}
