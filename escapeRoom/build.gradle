plugins {
    id 'java-library'
    id 'application' // necessary to use installDist task
    id 'org.beryx.runtime' version '2.0.1' // Java badass runtime
}

application {
    mainClass = "starter.MushRoom"
    applicationName = "escapeRoom"
}


dependencies {
    api project(':dungeon')

    // JUnit and Mockito for testing
    testImplementation supportDependencies.junit
    testRuntimeOnly supportDependencies.junitLauncher
    testImplementation supportDependencies.mockito_core
}

sourceSets.main.java.srcDirs = ['src/']
sourceSets.main.resources.srcDirs = ['assets/']
sourceSets.test.java.srcDirs = ['test/']

tasks.register('runCoopDungeon', JavaExec) {
    group 'starter'
    mainClass = 'starter.CoopDungeon'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runDemoRoom', JavaExec) {
    group 'starter'
    mainClass = 'starter.DemoRoom'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runSpriteTestRoom', JavaExec) {
    group 'starter'
    mainClass = 'starter.SpriteTestRoom'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runMushRoom', JavaExec) {
    group 'starter'
    mainClass = 'starter.MushRoom'
    classpath = sourceSets.main.runtimeClasspath
}

processResources {
    from new File(project(':dungeon').projectDir, '/assets')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

// using the fat jar from old task buildDemonRun
tasks.named('jar', Jar) {
    dependsOn project(':dungeon').tasks.named('jar')
    //group = 'jar'
    archiveBaseName.set('EscaperoomDemo')
    archiveFileName.set('EscapeRoomDemo.jar')

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // include compiled classes
    from sourceSets.main.output
    from project(':dungeon').sourceSets.main.output

    // include dependencies
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    // include assets
    into('assets') {
        from new File(project(':dungeon').projectDir, '/assets')
    }

    // manifest
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }

    // exclude signature files
    exclude('META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
}

tasks.named('jpackageImage'){
    dependsOn 'installDist'
}


/*
    "Creates an image containing your application, a custom JRE, and appropriate start scripts.
    If the property distDir is not set, this task depends on either installDist or installShadowDist"

    runtime(Config Block)
    Configuration block for org.beryx.runtime plugin(Java Badass Runtime)
    This plugin creates a custom JRE (using jlink) and packages it with the application (using jpackage)
*/
runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']

    // Java modules to include in the custom JRE (only these will be bundled, reducing size)
    modules = [
        'java.base',
        'java.desktop',
        'java.logging',
        'java.naming',
        'jdk.unsupported',
        'jdk.zipfs'
    ]

    // Configuration for jpackage tool
    jpackage {

        // Directory containing all JARs (created by installDist task)    "../build/install/dungeon/lib"
        def inputDir = layout.buildDirectory.dir("install/${application.applicationName}/lib").get().asFile

        // Options passed to jpackage tool (jpackage requires --input option to know where all jars are)
        imageOptions = ["--input", inputDir.absolutePath]

    }
}

