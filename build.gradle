buildscript {
    apply from: 'dependencies.gradle'
}


plugins {
    id 'com.diffplug.spotless' version "${spotlessVersion}"
    id 'com.github.jk1.dependency-license-report' version "${licenseReportVersion}"
}


allprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
        sourceCompatibility = JavaVersion.VERSION_21
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaExec).configureEach {
        dependsOn classes
        standardInput = System.in
        ignoreExitValue true
        systemProperty 'BASELOGDIR', project.baseLogDir
    }

    clean {
        delete project.baseLogDir
    }

    checkstyle {
        configFile = rootProject.file('.checkstyle.xml')
        // Default version vs. current version? The default version is quite old (9.3 from
        // Jan 30, 2022), so let's go with the current version (10.14).
        // However, this needs to be updated manually as Dependabot won't deal with this!
        toolVersion = '10.16.0'
        ignoreFailures = false
        maxWarnings = 0
    }
    // Workaround to resolve CS dependencies: https://github.com/checkstyle/checkstyle/issues/14123#issuecomment-1961029772
    configurations.checkstyle {
        resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
            select("com.google.guava:guava:0")
        }
    }
}


spotless {
    java {
        targetExclude('build/generated-src/**')
        googleJavaFormat()

        target '**/*.java'
    }
}


import com.github.jk1.license.render.*

licenseReport {
    // By default this plugin will collect the union of all licenses from
    // the immediate pom and the parent poms. If your legal team thinks this
    // is too liberal, you can restrict collected licenses to only include the
    // those found in the immediate pom file
    // Defaults to: true
    unionParentPomLicenses = false

    // Set output directory for the report data.
    // Defaults to ${project.buildDir}/reports/dependency-license.
    outputDir = "$projectDir/licenses"

    // Select projects to examine for dependencies.
    // Defaults to current project and all its subprojects
    projects = [project] + project.subprojects

    // Adjust the configurations to fetch dependencies. Default is 'runtimeClasspath'
    // For Android projects use 'releaseRuntimeClasspath' or 'yourFlavorNameReleaseRuntimeClasspath'
    // Use 'ALL' to dynamically resolve all configurations:
    // configurations = ALL
    configurations = ['runtimeClasspath']

    // Don't include artifacts of project's own group into the report
    excludeOwnGroup = true

    // Don't exclude bom dependencies.
    // If set to true, then all boms will be excluded from the report
    excludeBoms = false

    // Set custom report renderer, implementing ReportRenderer.
    // Yes, you can write your own to support any format necessary.
    renderers = [new InventoryMarkdownReportRenderer('third-party-libs.md', 'Back-End Libraries')]
}
