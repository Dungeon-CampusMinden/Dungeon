plugins {
    // we can not use ext variables here
    id "java"
    id "checkstyle"
    id "com.github.spotbugs" version "5.0.+"
    id "com.diffplug.spotless" version "6.5.+"
    id 'antlr'
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    gradlePluginPortal()
    google()
}

ext {
    // we can not use the + operator here
    gdxVersion = "1.10.1-SNAPSHOT"
    aiVersion = "1.8.2"
    gsonVersion = "2.9.0"
}

dependencies {
    // LibGDX
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-ai:$aiVersion"

    // JUnit 4, Mockito and Powermock for testing:
    // https://mvnrepository.com/artifact/junit/junit
    testImplementation "junit:junit:4.+"

    // https://mvnrepository.com/artifact/org.powermock/powermock-module-junit4
    testImplementation "org.powermock:powermock-module-junit4:2.+"
    // https://mvnrepository.com/artifact/org.powermock/powermock-api-mockito2
    testImplementation "org.powermock:powermock-api-mockito2:2.+"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: "com.google.code.gson", name: "gson", version: gsonVersion

    // ANTLR version 4 for DSL Grammar
    antlr "org.antlr:antlr4:4.11.1"
}

sourceCompatibility = 17
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"
sourceSets.main.java.srcDirs = ["game/src/", "dsl/src/", "dsl-connector/src", "$projectDir/build/generated-src/"]
sourceSets.main.resources.srcDirs = ["game/assets/"]
sourceSets.test.resources.srcDirs = ["dsl/test_resources"]
sourceSets.test.java.srcDirs = ["game/test/", "dsl/test/", "dsl-connector/test"]
sourceSets.main.antlr.srcDirs = ["dsl/src/antlr"]

project.ext.mainClassName = "starter.Game"
project.ext.assetsDir = new File("game/assets")

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        allJvmArgs += ["-XstartOnFirstThread"]
    }
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}

task debug(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    debug = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        allJvmArgs += ["-XstartOnFirstThread"]
    }
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

checkstyle {
    toolVersion = "10.2"
    configFile = file("checks.xml")
    showViolations = true
}

spotbugs {
    ignoreFailures = true
    showStackTraces = false
    reportLevel = "low"
}

spotbugsMain {
    reports {
        xml {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.xml")
        }
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = "fancy-hist.xsl"
        }
    }
}

spotbugsTest {
    reports {
        xml {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/test/spotbugs.xml")
        }
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = "fancy-hist.xsl"
        }
    }
}

spotless {
    java {
        targetExclude("build/generated-src/**")
        googleJavaFormat().aosp()
    }
}

// generate ANTLR visitor and listener classes from grammar
generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-listener" ]
}
