plugins {
    // we can not use ext variables here
    id "application"
    id "com.diffplug.spotless" version "6.23.3"
    id "antlr"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    sourceCompatibility = JavaVersion.VERSION_17
}

// store name of method parameter names in .class files (used to build
// DSL-adapter types, which contain the names of the parameters as members)
tasks.withType(JavaCompile).configureEach {
    configure(options) {
        options.compilerArgs << "-parameters"
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    gradlePluginPortal()
    google()
}

ext {
    // we can not use the + operator here
    gdxVersion = "1.12.1"
}

dependencies {
    // LibGDX
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-ai:1.8.2"

    // JUnit 4, Mockito and Powermock for testing:
    // https://mvnrepository.com/artifact/junit/junit
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:5.8.0"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation "com.google.code.gson:gson:2.10.1"

    // ANTLR version 4 for DSL Grammar
    antlr "org.antlr:antlr4:4.13.1"
}

[compileJava, compileTestJava]*.options*.encoding = "UTF-8"
sourceSets.main.java.srcDirs = ["game/src/", "dungeon/src/", "blockly/src/", "$projectDir/build/generated-src/"]
sourceSets.main.resources.srcDirs = ["game/assets/", "dungeon/assets/", "blockly/assets/"]
sourceSets.test.resources.srcDirs = ["dungeon/assets/", "game/test_resources", "dungeon/test_resources"]
sourceSets.test.java.srcDirs = ["game/test/", "dungeon/test"]
sourceSets.main.antlr.srcDirs = ["dungeon/src/dsl/antlr"]

project.ext.mainClassName = "starter.BasicStarter"
project.ext.assetsDir = new File("game/assets")

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

tasks.register("printOperatingSystem") {
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}

application {
    mainClass = project.mainClassName
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

tasks.register("runRandom", JavaExec) {
    dependsOn classes
    mainClass = "starter.RandomDungeon"
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

tasks.register("start", JavaExec) {
    dependsOn classes
    mainClass = "starter.Starter"
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

tasks.register("blockly", JavaExec) {
    dependsOn classes
    mainClass = "client.Client"
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

tasks.register("debug", JavaExec) {
    dependsOn classes
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    debug = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

spotless {
    java {
        targetExclude("build/generated-src/**")
        googleJavaFormat()
    }
}


// generate ANTLR visitor and listener classes from grammar
generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-listener"]
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes "Main-Class": project.mainClassName
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*module-info.class"
}

tasks.register("starterJar", Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes "Main-Class": "starter/Starter"
    }

    archiveFileName = "Starter.jar"

    from sourceSets.main.output

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*module-info.class"
}

//MANUAL TESTS

tasks.register("runManualQuizTests", JavaExec) {
    dependsOn classes
    mainClass = "manual.quizquestion.QuizQuestionUITest"
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

tasks.register("runYesNoDialogTests", JavaExec) {
    dependsOn classes
    mainClass = "manual.YesNoDialogTest"
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

tasks.register("runCallbackTests", JavaExec) {
    dependsOn classes
    mainClass = "manual.quizquestion.CallbackTest"
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

tasks.register("runLoaderTests", JavaExec) {
    dependsOn classes
    mainClass = "manual.dslFileReader.DslFileReaderTest"
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

tasks.register("taskGenerationTests", JavaExec) {
    dependsOn classes
    mainClass = "manual.taskgeneration.TaskGenerationTest"
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
}

run.dependsOn(printOperatingSystem)
runRandom.dependsOn(printOperatingSystem)
start.dependsOn(printOperatingSystem)
blockly.dependsOn(printOperatingSystem)
debug.dependsOn(printOperatingSystem)
runManualQuizTests.dependsOn(printOperatingSystem)
runYesNoDialogTests.dependsOn(printOperatingSystem)
runCallbackTests.dependsOn(printOperatingSystem)
runLoaderTests.dependsOn(printOperatingSystem)
taskGenerationTests.dependsOn(printOperatingSystem)
