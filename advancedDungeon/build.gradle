plugins {
    id 'java-library'
    id 'application' // necessary to use installDist task
    id 'org.beryx.runtime' version '2.0.1' // Java badass runtime
}

application {
    mainClass = "aiAdvanced.starter.PathfinderStarter"
    applicationName = "advancedDungeon"
}

dependencies {
    implementation project(':dungeon')
}

sourceSets.main.java.srcDirs = ['src/']
sourceSets.main.resources.srcDirs = ['assets/']

processResources {
    from new File(project(':dungeon').projectDir, '/assets')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}


tasks.register('runPortal', JavaExec) {
    group 'starter'
    mainClass = 'starter.PortalStarter'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runPathfinder', JavaExec) {
    group 'starter'
    mainClass = 'aiAdvanced.starter.PathfinderStarter'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runPathFindingComparison', JavaExec) {
    group 'starter'
    mainClass = 'aiAdvanced.starter.ComparePathfindingStarter'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.named('clean') {
    delete(
            file("$rootDir/currentAdvancedLevel.json"),
            file("$rootDir/currentPortalLevel.json"),
    )
}

// using the fat jar from old task buildDemonRun
tasks.named('jar', Jar) {
    dependsOn project(':dungeon').tasks.named('jar')

    archiveBaseName.set('advancedDungeon')
    archiveFileName.set('advancedDungeon.jar')

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // include compiled classes
    from sourceSets.main.output
    from project(':dungeon').sourceSets.main.output

    // include dependencies
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }


    // include assets
    into('assets') {
        from new File(project(':dungeon').projectDir, '/assets')
    }

    // manifest
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }

    // exclude signature files
    exclude('META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
}

tasks.named('jpackageImage'){
    dependsOn 'installDist'
}


/*
    "Creates an image containing your application, a custom JRE, and appropriate start scripts.
    If the property distDir is not set, this task depends on either installDist or installShadowDist"

    runtime(Config Block)
    Configuration block for org.beryx.runtime plugin(Java Badass Runtime)
    This plugin creates a custom JRE (using jlink) and packages it with the application (using jpackage)
*/
runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']

    // Java modules to include in the custom JRE (only these will be bundled, reducing size)
    modules = [
        'java.base',
        'java.desktop',
        'java.logging',
        'java.naming',
        'jdk.unsupported',
        'jdk.zipfs',
        'java.compiler',
    ]

    // Configuration for jpackage tool
    jpackage {

        // Directory containing all JARs (created by installDist task)    "../build/install/dungeon/lib"
        def inputDir = layout.buildDirectory.dir("install/${application.applicationName}/lib").get().asFile

        // Options passed to jpackage tool (jpackage requires --input option to know where all jars are)
        imageOptions = ["--input", inputDir.absolutePath]

    }
}




