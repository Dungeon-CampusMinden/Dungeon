plugins {
    id 'java-library'
}

dependencies {
    // Abh채ngigkeit zum Hauptprojekt "dungeon"
    api project(':dungeon')

    // LibGDX-Abh채ngigkeiten
    implementation "com.badlogicgames.gdx:gdx:1.12.0"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:1.12.0"
    implementation "com.badlogicgames.gdx:gdx-platform:1.12.0:natives-desktop"
}

sourceSets.main.java.srcDirs = ['src/']
sourceSets.main.resources.srcDirs = ['assets/']

// Task zum Ausf체hren der DevDungeon-Klasse
tasks.register('runDevDungeon', JavaExec) {
    group = 'starter'
    mainClass = 'starter.DevDungeon'
    classpath = sourceSets.main.runtimeClasspath
}

// Task zum Erstellen eines ausf체hrbaren JARs
tasks.register('buildDevDungeonJar', Jar) {
    group = 'jar'
    dependsOn ':dungeon:jar'
    archiveBaseName = 'DevDungeon'
    from sourceSets.main.output
    from project(':dungeon').sourceSets.main.output
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'starter.DevDungeon'
    }

    archiveFileName = 'DevDungeon.jar'

    into('assets') {
        from new File(project(':dungeon').projectDir, '/assets')
    }

    exclude('META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
}

// Ressourcen aus Hauptprojekt mit einbinden
processResources {
    from new File(project(':dungeon').projectDir, '/assets')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
