import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java-library'
    id 'antlr'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    gradlePluginPortal()
    google()
}

dependencies {
    api project(":game")

    testImplementation supportDependencies.junit
    testImplementation supportDependencies.mockito_core

    // ANTLR version 4 for DSL Grammar
    antlr supportDependencies.antlr
}

sourceSets.main.java.srcDirs = ["src/", "$projectDir/build/generated-src/antlr/main/"]
sourceSets.test.java.srcDirs = ["test/"]
sourceSets.main.resources.srcDirs = ["assets/"]
sourceSets.test.resources.srcDirs = ["test_resources/", "assets/"]
sourceSets.main.antlr.srcDirs = ["src/dsl/antlr"]


// generate ANTLR visitor and listener classes from grammar
generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-listener" ]
}

tasks.register('start', JavaExec) {
    dependsOn 'classes'
    mainClass = "starter.Starter"
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}

tasks.register('starterJar', org.gradle.jvm.tasks.Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': "starter/Starter"
    }

    archiveFileName = 'Starter.jar'

    from sourceSets.main.output

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', "META-INF/*module-info.class"
}

tasks.register('runManualQuizTest', JavaExec) {
    dependsOn 'classes'
    mainClass = "manual.quizquestion.QuizQuestionUITest"
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}

tasks.register('runCallbackTest', JavaExec) {
    dependsOn 'classes'
    mainClass = "manual.quizquestion.CallbackTest"
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}

tasks.register('runTaskGenerationTest', JavaExec) {
    dependsOn 'classes'
    mainClass = "manual.taskgeneration.TaskGenerationTest"
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        jvmArgs("-XstartOnFirstThread")
    }
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}
