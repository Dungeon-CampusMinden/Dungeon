plugins {
    id 'java-library'
    id 'application'

}
apply plugin: 'io.github.fourlastor.construo'

import io.github.fourlastor.construo.Target

dependencies {
    // LibGDX: expose this API to users because of core.level.elements.ILevel
    api supportDependencies.gdx
    api supportDependencies.gdx_platform
    api supportDependencies.gdx_backend_lwjgl3
    api supportDependencies.gdx_lwjgl3_glfw_awt_macos
    api supportDependencies.gdx_ai
    api supportDependencies.gdx_freetype
    api supportDependencies.gdx_freetype_platform
    api supportDependencies.gdx_backend_headless

    // Netty for server-client communication
    implementation supportDependencies.netty_all

    // JUnit and Mockito for testing
    testImplementation supportDependencies.junit
    testImplementation supportDependencies.junitLauncher
    testRuntimeOnly supportDependencies.junitLauncher
    testImplementation supportDependencies.mockito_core
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl:1.14.0"
    testImplementation("com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion")
    testImplementation("com.badlogicgames.gdx:gdx:$gdxVersion")
}


sourceSets.main.java.srcDirs = ['src/']
sourceSets.main.resources.srcDirs = ['assets/']

sourceSets.test.java.srcDirs = ['test/']
sourceSets.test.resources.srcDirs = ['test_assets/']

tasks.register('runBasicStarter', JavaExec) {
    group 'starter'
    mainClass = 'starter.BasicStarter'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('debugBasicStarter', JavaExec) {
    group 'starter'
    mainClass = 'starter.BasicStarter'
    classpath = sourceSets.main.runtimeClasspath
    debug = true
}

tasks.register('runMPServer', JavaExec) {
    group 'starter'
    mainClass = 'starter.MultiplayerServer'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runMPClient', JavaExec) {
    group 'starter'
    mainClass = 'starter.MultiplayerClient'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

// Application plugin config
application {
    mainClass = 'starter.BasicStarter'
}

// Construo config
construo {
    name.set("Dungeon")
    humanName.set("Dungeon")
    outputDir.set(file("${buildDir}/dist"))

    targets {
        create("linuxX64", Target.Linux) {
            architecture.set(Target.Architecture.X86_64)
            jdkUrl.set("https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.4%2B7/OpenJDK21U-jdk_x64_linux_hotspot_21.0.4_7.tar.gz")
        }
        create("winX64", Target.Windows) {
            architecture.set(Target.Architecture.X86_64)
            jdkUrl.set("https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.4%2B7/OpenJDK21U-jdk_x64_windows_hotspot_21.0.4_7.zip")
        }
        create("macX64", Target.MacOs) {
            architecture.set(Target.Architecture.X86_64)
            jdkUrl.set("https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.4%2B7/OpenJDK21U-jdk_x64_mac_hotspot_21.0.4_7.tar.gz")
            identifier.set("de.dungeon.game")
        }
        create("macM1", Target.MacOs) {
            architecture.set(Target.Architecture.AARCH64)
            jdkUrl.set("https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.4%2B7/OpenJDK21U-jdk_aarch64_mac_hotspot_21.0.4_7.tar.gz")
            identifier.set("de.dungeon.game")
        }
    }
}
