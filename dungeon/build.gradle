plugins {
    id 'java-library'
    id 'com.google.protobuf'
    id 'application' // necessary to use installDist task
    id 'org.beryx.runtime' version '2.0.1' // Java badass runtime
}

application {
    mainClass = "starter.BasicStarter"
    applicationName = "dungeon"
}

dependencies {
    // LibGDX: expose this API to users because of core.level.elements.ILevel
    api supportDependencies.gdx
    api supportDependencies.gdx_platform
    api supportDependencies.gdx_backend_lwjgl3
    api supportDependencies.gdx_lwjgl3_glfw_awt_macos
    api supportDependencies.gdx_ai
    api supportDependencies.gdx_freetype
    api supportDependencies.gdx_freetype_platform
    api supportDependencies.gdx_backend_headless

    // Netty for server-client communication
    implementation supportDependencies.netty_all

    // Protobuf for network serialization
    api supportDependencies.protobuf_java
    implementation supportDependencies.protobuf_java_util

    // JUnit and Mockito for testing
    testImplementation supportDependencies.junit
    testImplementation supportDependencies.junitLauncher
    testRuntimeOnly supportDependencies.junitLauncher
    testImplementation supportDependencies.mockito_core
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl:1.14.0"
    testImplementation("com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion")
    testImplementation("com.badlogicgames.gdx:gdx:$gdxVersion")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java { }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/', "$buildDir/generated/source/proto/main/java"]
        }
        resources {
            srcDirs = ['assets/']
        }
        proto {
            srcDir 'proto'
        }
    }
    test {
        java {
            srcDirs = ['test/']
        }
        resources {
            srcDirs = ['test_assets/']
        }
    }
}

tasks.named('compileJava') {
    dependsOn 'generateProto'
}

tasks.register('runBasicStarter', JavaExec) {
    group 'starter'
    mainClass = 'starter.BasicStarter'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('debugBasicStarter', JavaExec) {
    group 'starter'
    mainClass = 'starter.BasicStarter'
    classpath = sourceSets.main.runtimeClasspath
    debug = true
}

tasks.register('runMPServer', JavaExec) {
    group 'starter'
    mainClass = 'starter.MultiplayerServer'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runMPClient', JavaExec) {
    group 'starter'
    mainClass = 'starter.MultiplayerClient'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

tasks.named('checkstyleMain') {
    exclude 'build/generated/source/proto/**'
    exclude '**/core/network/proto/**'
}

tasks.named('checkstyleTest') {
    exclude 'build/generated/source/proto/**'
    exclude '**/core/network/proto/**'
}

/*
    this task creates the jar files based on compiled classes from .java files and assets( comes from java-library plugin)
    manifest attribute is set to the main class of the project.
    It provides the main class information to jpackage for creating the native executable
*/
tasks.named('jar', Jar) {
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
}

/*
    "Uses the jpackage tool to create a platform-specific application image.
    depends on: runtime"

    - installDist is a task called from jpackageImage task. it uses the jar files to create a proper directory structure with all necessary jar files and assets
    - packageImage task (from org.beryx.runtime plugin) uses the JDK's jpackage tool (configured in the runtime.jpackage block) to create the executable of project with bundled JRE
 */
tasks.named('jpackageImage') {
    dependsOn 'installDist'
}


/*
    "Creates an image containing your application, a custom JRE, and appropriate start scripts.
    If the property distDir is not set, this task depends on either installDist or installShadowDist"

    runtime(Config Block)
    Configuration block for org.beryx.runtime plugin(Java Badass Runtime)
    This plugin creates a custom JRE (using jlink) and packages it with the application (using jpackage)
*/
runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']

    // Java modules to include in the custom JRE (only these will be bundled, reducing size)
    modules = [
        'java.base',
        'java.desktop',
        'java.logging',
        'java.naming',
        'jdk.unsupported',
        'jdk.zipfs'
    ]

    // Configuration for jpackage tool
    jpackage {

        // Directory containing all JARs (created by installDist task)    "../build/install/dungeon/lib"
        def inputDir = layout.buildDirectory.dir("install/${application.applicationName}/lib").get().asFile

        // Options passed to jpackage tool (jpackage requires --input option to know where all jars are)
        imageOptions = ["--input", inputDir.absolutePath]

    }
}
